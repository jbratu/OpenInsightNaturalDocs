Function CS_NATURALDOCS_LIB(inMethod, Param1, Param2, Param3, Param4, outValue,outStat)
/*
	Function: CS_NATURALDOCS_LIB
--- Code	
CS_NATURALDOCS_LIB(inMethod, Param1, Param2, Param3, Param4, outValue,outStat)	
---

Library For generating OpenInsight BASIC+ source code documentation With Natural Docs

	Parameters:
	inMethod - Varies depending On action.
	Param1...Param4 - Varies depending On inMethod.
	outValue - Unused. Optional Return value by reference.
	outStat - Unused. Optional Return status by reference.
	
Revisions:
- History	(Date, Initials, Notes)
- 06/07/18	JAB-CS  Update For NaturalDocs 2.x
- 01/11/17	JAB-CS	Revised documentation And added constants.
- 11/26/16	JAB-CS	Added ability To specify list of routines To export
- 08/30/16	JAB-CS	Proof of concept #AUTO_GENERATE_DOCS

*/
If Unassigned(inMethod) Then method = '' Else method = inMethod
If Unassigned(Param1) Then Param1 = ''
If Unassigned(Param2) Then Param2 = ''
If Unassigned(Param3) Then Param3 = ''
If Unassigned(Param4) Then Param4 = ''
If Unassigned(outValue) Then outValue = ''
If Unassigned(outStat) Then outStat = ''
RetVal = ''

*
* Declare System
Declare Function Assigned, Msg, Get_Property, RTI_WINAPI_ShellExecuteEx
Declare Subroutine Msg, Copy_Row_To_OS, Set_Status, RLIST

$INSERT Msg_Equates
$Insert Logical

*
* Declare App
Err = ''

/*const: BaseDir
Default directory For export. C:\naturaldocsplus\oisrc\ */
BaseDir = "C:\NaturalDocsTest\oisrc\"

/* const: NaturalDocsEXE
Location of Natural Docs program To generated exported files as HTML */
NaturalDocsEXE = "C:\Program Files (x86)\Natural Docs\NaturalDocs.exe"
NaturalDocsArgs = "-i C:\NaturalDocsTest\oisrc -p C:\NaturalDocsTest\oiproj -o HTML C:\NaturalDocsTest\DOCS -r"

/* const: GeneratedDocumentationFile
Location of file generated by Natural Docs */
GeneratedDocumenationFile = "C:\NaturalDocsTest\DOCS\index.html"

*
*Main case branch
*
A_CommuterModuleTop:
Begin Case
	
	Case Method _eqc 'generateDocs'		; Gosub generateDocs_
	
	Case Otherwise$
	
		A_CommuterModuleOtherwise:
		
		Gosub generateDocs_
		
	Goto End
End Case

End:
Return RetVal
*
*End of the main branch
*

/*
	interface: generateDocs
	
	Export routines For generation of documentation.
	Export all tagged routines:
	--- Code
	CS_NATURALDOCS_LIB('generateDocs')
	---
	
	Export selected routines:
	--- Code
	run CS_NATURALDOCS_LIB 'generateDocs', 'YOUR_ROUTINE*APPID'
	---
	
	Parameters:
	None
	
	Returns:
	None - Displays confirmation message showing number of routines exported
	
*/
generateDocs_:
	
	SRCLIST = Param1
	
	If SRCLIST EQ '' Then
		*No list of documents was specified so generate all tagged docs
		
		/* const: Query
		Search For source code With the tag To indicate it should be exported
		for document generation:
		SELECT SYSPROCS WITH ALL CONTAINING "#AUTO_GENERATE_DOCS"
		*/
		Query = 'SELECT SYSPROCS WITH ALL CONTAINING "#AUTO_GENERATE_DOCS"'
		
		Set_Status(0)
		Rlist(Query, 5, '','','')
		If Get_Status(Err) Then
			RetVal = ''
			Return
		End
		
		Hits = ''
		
		//Build a list of the keys
		Done = False$
		Loop
			Readnext @ID Else Done = True$
		Until Done Do
			SRCLIST<-1> = @ID
		Repeat
	End Else
		*Format the specified list of docs
		Swap ',' With @FM In SRCLIST
	End
	
	SrcCount = DCount(SRCLIST,@FM)
	
	//Write each of the records to a text file for natrual docs to read
	For i = 1 To SrcCount

		Key = SRCLIST<i>
		DestFile = BaseDir : SRCLIST<i> : ".bp"
		
		//OS files can't support asterisks in file names.
		Swap "*" With "-" In DestFile
		
		AlwaysOverwrite = 2
		ConvertLineMarks = 0
		Copy_Row_To_OS('SYSPROCS', Key, DestFile, AlwaysOverwrite, ConvertLineMarks, 0 )
	Next

	*
	* Execute NaturalDocs to generate it
	*	                
	hwnd=get_Property( @window, "HANDLE" )
	lpOperation="open" : \00\
	lpFile=NaturalDocsEXE : \00\ 
	lpParameters=NaturalDocsArgs : \00\
	lpDirectory='' : \00\
	nShowCmd=1
	privShellExecute=RTI_WINAPI_ShellExecuteEx( hwnd, lpOperation, lpFile, lpParameters, lpDirectory, nShowCmd )

	*
	* Show the file
	*
	Call Utility("RUNWIN", GeneratedDocumenationFile, 1)


Return